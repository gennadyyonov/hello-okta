plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

allprojects {
	repositories {
		mavenCentral()
	}

	ext {
		set('springBootVersion', "2.2.4.RELEASE")
		set('springCloudVersion', "Hoxton.RELEASE")
		set('oktaSpringBootStarterVersion', "1.3.0")
		set('lombokVersion', "1.18.10")
		set('feignVersion', "10.7.4")
		set('commonsLang3Version', "3.9")
		set('aspectJWeaverVersion', "1.9.5")
		set('orgSpringframeworkVersion', "5.2.3.RELEASE")
		set('orgSpringframeworkSecurityVersion', "5.2.1.RELEASE")
		set('orgSpringframeworkSecurityJwtVersion', "1.0.9.RELEASE")
		set('fasterxmlJacksonCoreVersion', "2.10.2")
		set('slf4jVersion', "1.7.30")
		set('servletApiVersion', "4.0.1")
	}
}

subprojects {
	group = 'lv.gennadyyonov'
	version = '0.0.1-SNAPSHOT'
}

configure(subprojects) {
	apply plugin: 'java'

	sourceCompatibility = '11'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	test {
		useJUnitPlatform()
	}
}

configure([ project(':hello-okta-api'), project(':hello-okta-bff') ]) {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	buildscript {
		dependencyManagement {
			imports {
				mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
			}
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation "com.okta.spring:okta-spring-boot-starter:${oktaSpringBootStarterVersion}"
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		compile "org.springframework.cloud:spring-cloud-starter-sleuth"

		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
		testImplementation 'org.springframework.security:spring-security-test'
	}
}
