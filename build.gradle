plugins {
    id 'java'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '7.0.2' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }

    ext {
        set('apacheHttpClientVersion', '4.5.14')
        set('commonsIoVersion', '2.14.0')
        set('commonsCompressVersion', '1.26.0')
        set('jsonwebtokenVersion', '0.12.6')
        set('micrometerTracingVersion', 'latest.release')
        set('nimbusdsVersion', '10.0.2')
        set('openApiVersion', '2.8.6')
        set('springBootVersion', '3.4.4')
        set('springCloudVersion', '2024.0.1')
        set('springDependencyManagementVersion', '1.1.7')
        set('wirmockVersion', '3.12.1')
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

subprojects {
    group = 'lv.gennadyyonov.hello'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'checkstyle'

    spotless {
        java {
            googleJavaFormat()
            importOrder('', 'javax', 'java', '\\#')
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    checkstyle {
        toolVersion = '10.21.4'
        configProperties.put('org.checkstyle.google.suppressionfilter.config', rootProject.file("config/checkstyle/checkstyle-suppressions.xml"))
        maxWarnings = 0
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = true
            html.required = true
        }
    }

    compileJava.dependsOn 'spotlessCheck'
    tasks.compileJava.finalizedBy tasks.checkstyleMain
    tasks.compileTestJava.finalizedBy tasks.checkstyleTest

    configurations {
        implementation {
            // Low CVE-200, CVE-2020-15250 fix
            exclude group: 'junit', module: 'junit'
        }
    }
}

