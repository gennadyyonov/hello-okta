plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

allprojects {
	repositories {
		mavenCentral()
	}

	ext {
		set('springCloudVersion', "Hoxton.RELEASE")
		set('oktaSpringBootStarterVersion', "1.3.0")
		set('lombokVersion', "1.18.10")
		set('commonsLang3Version', "3.9")
		set('swaggerVersion', "2.9.2")
	}
}

subprojects {
	group = 'lv.gennadyyonov'
	version = '0.0.1-SNAPSHOT'
}

configure(subprojects) {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.springframework.boot'

	buildscript {
		dependencyManagement {
			imports {
				mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
			}
		}
	}

	sourceCompatibility = '11'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	test {
		useJUnitPlatform()
	}

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.springframework.security:spring-security-test'
    }

	bootJar {
		enabled = false
	}
	jar {
		enabled = true
	}
}

configure([ project(':hello-okta-api'), project(':hello-okta-bff') ]) {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation "com.okta.spring:okta-spring-boot-starter:${oktaSpringBootStarterVersion}"
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		compile "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure"
		compile "org.springframework.cloud:spring-cloud-starter-sleuth"
		compile "io.springfox:springfox-swagger2:${swaggerVersion}"
		compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"

		implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
		compile "io.github.openfeign:feign-jackson"
		compile "io.github.openfeign:feign-httpclient"

		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
	}

	bootJar {
		enabled = true
	}
	jar {
		enabled = false
	}
}
