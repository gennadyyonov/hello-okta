plugins {
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'org.springframework.boot' version "${springBootVersion}"
}

sourceSets {
    main
    localhost {
        compileClasspath += main.output
        runtimeClasspath += main.output
        annotationProcessorPath += main.output
    }
}

configurations {
    localhostImplementation.extendsFrom implementation
    localhostCompileOnly.extendsFrom compileOnly
    localhostAnnotationProcessor.extendsFrom annotationProcessor
}

task runLocal(type: Exec, dependsOn: bootRun) {
    group = 'application'

    description = 'Run API LOCALHOST'

    doFirst() {
        println('Running API LOCALHOST...')
    }
}

bootRun {
    def localPropertiesFile = 'src/localhost/resources/localhost.properties';
    file(localPropertiesFile).readLines().each() {
        if (!it.isEmpty() && !it.startsWith('#')) {
            def (key, value) = it.tokenize('=')
            if (System.getenv(key) == null) {
                environment key, value
            }
        }
    }
    systemProperties System.properties
}

dependencies {
    // Import BOM-s
    implementation enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation project(':hello-okta-api-dto')
    implementation project(':hello-okta-logging-api')
    implementation project(':hello-okta-logging-impl')
    implementation project(':hello-okta-services')
    implementation project(':hello-okta-actuator')

    implementation "io.springfox:springfox-boot-starter:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "com.okta.spring:okta-spring-boot-starter:${oktaSpringBootStarterVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.openfeign:feign-jackson'
    implementation 'io.github.openfeign:feign-httpclient'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    if (gradle.startParameter.taskNames.contains('runLocal') ) {
        println('LOCALHOST API Dependencies')
        runtimeClasspath sourceSets.localhost.output
    }
}

test {
    useJUnitPlatform()
}